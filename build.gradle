buildscript {
	repositories {
		maven { url 'https://jitpack.io' }
	}

	dependencies {
		classpath 'com.github.kiview:captain-hook:76a1c11a16'
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.1'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'com.diffplug.spotless' version '6.14.0'
}

apply plugin: 'com.github.tjni.captainhook'

captainHook {
	preCommit = './gradlew staging spotlessApply'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('testcontainersVersion', "1.17.6")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.springframework.pulsar:spring-pulsar-reactive-spring-boot-starter:0.1.0'
	compileOnly 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testImplementation 'io.projectreactor.tools:blockhound:1.0.7.RELEASE'
	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'org.postgresql:r2dbc-postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:pulsar'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'org.testcontainers:r2dbc'
}

dependencyManagement {
	imports {
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.withType(Test).all {
	if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_13)) {
		jvmArgs += [
			"-XX:+AllowRedefinitionToAddDeleteMethods"
		]
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

spotless {
	java {
		removeUnusedImports()

		prettier(['prettier': '2.5.1', 'prettier-plugin-java': '1.6.1'])
		.config([
			'parser'    : 'java',
			'tabWidth'  : 4,
			'printWidth': 120
		])

		importOrder('', 'java', 'javax', '\\#')
	}
	groovyGradle {
		greclipse('4.19.0')
	}
}